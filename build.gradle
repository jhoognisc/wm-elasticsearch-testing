buildscript {
    repositories {
        maven {
            url 'https://artifactory.nisc.coop/artifactory/libs-release'
            credentials {
                username = "${artifactoryUser}"
                password = "${artifactoryPassword}"
            }
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.15.2"
        classpath "coop.nisc.gitversion:git-version:1.3.0"
    }
}

defaultTasks 'clean', 'build'

apply plugin: "com.jfrog.artifactory"
apply plugin: 'coop.nisc.gitversion'

if (!project.hasProperty("artifactoryUrl")) {
    ext.artifactoryUrl = 'https://artifactory.nisc.coop/artifactory'
}

ext {
    gitBranch = rootProject.gitVersion.branch
    gitRevision = rootProject.gitVersion.revision

    remoteRepositoryUrl = artifactoryUrl

    applicationNamespace = "wm"
}

// Setting this property to true will make the artifactoryPublish task skip this module (in our case, the root module):
artifactoryPublish.skip = true

subprojects {

    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group = 'coop.nisc'
    version = rootProject.gitVersion.buildVersion

    repositories {
        maven {
            url "${remoteRepositoryUrl}/libs-release"
            credentials {
                username = "${artifactoryUser}"
                password = "${artifactoryPassword}"
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact "build/libs/${project.name}.jar"
            }
        }
    }

}

artifactory {

    clientConfig.setIncludeEnvVars(true)
    clientConfig.setEnvVarsExcludePatterns('*pass*,*secret*') // Don't publish sensitive vars to Artifactory
    clientConfig.info.setBuildNumber("${project.version}-${System.currentTimeMillis()}")

    contextUrl = "${remoteRepositoryUrl}"
    publish {
        repository {
            repoKey = 'nisc-libs-local'
            username = "${artifactoryUser}"
            password = "${artifactoryPassword}"
        }
        defaults {
            publications('mavenJava')
            publishArtifacts = true
            publishIvy = false
            publishPom = false // We don't build/publish a pom
        }
    }
}
